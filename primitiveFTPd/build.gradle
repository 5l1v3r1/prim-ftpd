buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath 'com.github.nrudenko:gradle-android-cq-plugin:0.1.7'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'
//apply plugin: 'android-cq'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "org.primftpd"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 47
        versionName "6.1-SNAPSHOT"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
        }
    }

    sourceSets {
        main {
            manifest {
                srcFile 'AndroidManifest.xml'
            }

            java {
                srcDir 'src'
            }
            res {
                srcDir 'res'
            }
        }
    }

    packagingOptions {
        // required due to jars
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/BCKEY.SF'
        exclude 'META-INF/BCKEY.DSA'
    }

    lintOptions {
        // we cannot be sure that all translations are present
        abortOnError false
    }
}

configurations {
    implementation.exclude group: 'org.slf4j', module: 'slf4j-api'
    implementation.exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
    implementation.exclude group: 'org.springframework'
}

dependencies {
    // print dependency tree:
    // ./gradlew primitiveFTPd:dependencies --configuration releaseCompileClasspath
    implementation project(':pftpd-pojo-lib')
    implementation project(':slf4j-android-1.6.1-RC1')
    implementation project(':apache-ftpserver-1.1.1:core')
    implementation project(':apache-ftpserver-1.1.1:ftplet-api')
    implementation project(':sshd-core-0.14.0')
    implementation 'org.apache.mina:mina-core:2.0.21'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.android.support:documentfile:28.0.0'
    implementation 'com.android.support:support-fragment:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'
}
